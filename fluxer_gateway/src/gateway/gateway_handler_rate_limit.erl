%% SPDX-License-Identifier: AGPL-3.0-or-later

-module(gateway_handler_rate_limit).
-typing([eqwalizer]).

-export([
    check_rate_limit/2,
    acquire_connection/1,
    release_connection/1,
    check_shared_ip_rate/1,
    check_shared_user_rate/1,
    note_disconnect/1
]).

-export_type([state/0]).

-define(GATEWAY_RATE_LIMIT_WINDOW_MS, 60000).
-define(GATEWAY_RATE_LIMIT_MAX_EVENTS, 600).
-define(PRESENCE_RATE_LIMIT_WINDOW_MS, 20000).
-define(PRESENCE_RATE_LIMIT_MAX_EVENTS, 5).

-define(SHARED_IP_RATE_TABLE, gateway_shared_ip_rate).
-define(SHARED_USER_RATE_TABLE, gateway_shared_user_rate).
-define(IP_CONNECTION_TABLE, gateway_ip_connections).

-define(SHARED_IP_RATE_WINDOW_MS, 60000).
-define(SHARED_IP_RATE_MAX_EVENTS, 6000).
-define(SHARED_USER_RATE_WINDOW_MS, 60000).
-define(SHARED_USER_RATE_MAX_EVENTS, 600).
-define(MAX_CONNECTIONS_PER_IP, 256).

-type state() :: gateway_handler:state().

-ifdef(TEST).
-include_lib("eunit/include/eunit.hrl").
-endif.

-spec check_rate_limit(state(), atom()) ->
    {ok, state()} | {rate_limited, state()} | {opcode_rate_limited, state()}.
check_rate_limit(State, Op) ->
    case rate_limits_disabled() of
        true -> {ok, State};
        false -> check_shared_then_connection(State, Op)
    end.

-spec check_shared_then_connection(state(), atom()) ->
    {ok, state()} | {rate_limited, state()} | {opcode_rate_limited, state()}.
check_shared_then_connection(State, Op) ->
    case check_shared_budgets(State) of
        rate_limited ->
            {rate_limited, State};
        ok ->
            check_rate_limit_limited(State, Op)
    end.

-spec check_shared_budgets(state()) -> ok | rate_limited.
check_shared_budgets(State) ->
    case check_shared_ip_rate(maps:get(peer_ip, State, undefined)) of
        {error, ip_rate_limited} ->
            rate_limited;
        ok ->
            check_shared_user_budget(maps:get(session_pid, State, undefined))
    end.

-spec check_shared_user_budget(term()) -> ok | rate_limited.
check_shared_user_budget(SessionPid) when is_pid(SessionPid) ->
    case check_shared_user_rate(SessionPid) of
        {error, user_rate_limited} -> rate_limited;
        ok -> ok
    end;
check_shared_user_budget(_) ->
    ok.

-spec check_rate_limit_limited(state(), atom()) ->
    {ok, state()} | {rate_limited, state()} | {opcode_rate_limited, state()}.
check_rate_limit_limited(#{rate_limit_state := RateLimitState} = State, Op) ->
    Now = erlang:system_time(millisecond),
    Events = maps:get(events, RateLimitState, []),
    case
        check_timestamp_window(
            Events,
            Now,
            ?GATEWAY_RATE_LIMIT_WINDOW_MS,
            ?GATEWAY_RATE_LIMIT_MAX_EVENTS
        )
    of
        rate_limited ->
            {rate_limited, State};
        {ok, NewEvents} ->
            NewRLS = RateLimitState#{events => NewEvents},
            check_opcode_rate_limit(State#{rate_limit_state => NewRLS}, Op, Now)
    end.

-spec check_opcode_rate_limit(state(), atom(), integer()) ->
    {ok, state()} | {opcode_rate_limited, state()}.
check_opcode_rate_limit(State, presence_update, Now) ->
    check_named_opcode_rate_limit(
        State,
        presence_update,
        Now,
        ?PRESENCE_RATE_LIMIT_WINDOW_MS,
        ?PRESENCE_RATE_LIMIT_MAX_EVENTS
    );
check_opcode_rate_limit(State, _Op, _Now) ->
    {ok, State}.

-spec check_named_opcode_rate_limit(state(), atom(), integer(), pos_integer(), pos_integer()) ->
    {ok, state()} | {opcode_rate_limited, state()}.
check_named_opcode_rate_limit(
    #{rate_limit_state := RateLimitState} = State,
    Op,
    Now,
    WindowMs,
    MaxEvents
) ->
    OpEvents = maps:get(op_events, RateLimitState, #{}),
    Events = maps:get(Op, OpEvents, []),
    case check_timestamp_window(Events, Now, WindowMs, MaxEvents) of
        rate_limited ->
            {opcode_rate_limited, State};
        {ok, NewEvents} ->
            NewOpEvents = OpEvents#{Op => NewEvents},
            {ok, State#{rate_limit_state => RateLimitState#{op_events => NewOpEvents}}}
    end.

-spec check_timestamp_window([integer()], integer(), pos_integer(), pos_integer()) ->
    {ok, [integer()]} | rate_limited.
check_timestamp_window(Events, Now, WindowMs, MaxEvents) ->
    EventsInWindow = [T || T <- Events, (Now - T) < WindowMs],
    case length(EventsInWindow) >= MaxEvents of
        true -> rate_limited;
        false -> {ok, [Now | EventsInWindow]}
    end.

-spec check_shared_ip_rate(term()) -> ok | {error, ip_rate_limited}.
check_shared_ip_rate(PeerIP) when is_binary(PeerIP), PeerIP =/= <<"unknown">> ->
    case rate_limits_disabled() of
        true ->
            ok;
        false ->
            ensure_window_table(?SHARED_IP_RATE_TABLE),
            check_shared_ip_window(PeerIP)
    end;
check_shared_ip_rate(_) ->
    ok.

-spec check_shared_ip_window(binary()) -> ok | {error, ip_rate_limited}.
check_shared_ip_window(PeerIP) ->
    case
        check_shared_window(
            ?SHARED_IP_RATE_TABLE,
            PeerIP,
            ?SHARED_IP_RATE_WINDOW_MS,
            ?SHARED_IP_RATE_MAX_EVENTS,
            ip_rate_limited
        )
    of
        ok -> ok;
        {error, ip_rate_limited} -> {error, ip_rate_limited}
    end.

-spec check_shared_user_rate(term()) -> ok | {error, user_rate_limited}.
check_shared_user_rate(UserKey) when UserKey =/= undefined ->
    case rate_limits_disabled() of
        true ->
            ok;
        false ->
            ensure_window_table(?SHARED_USER_RATE_TABLE),
            check_shared_user_window(UserKey)
    end;
check_shared_user_rate(_) ->
    ok.

-spec check_shared_user_window(term()) -> ok | {error, user_rate_limited}.
check_shared_user_window(UserKey) ->
    case
        check_shared_window(
            ?SHARED_USER_RATE_TABLE,
            UserKey,
            ?SHARED_USER_RATE_WINDOW_MS,
            ?SHARED_USER_RATE_MAX_EVENTS,
            user_rate_limited
        )
    of
        ok -> ok;
        {error, user_rate_limited} -> {error, user_rate_limited}
    end.

-spec check_shared_window(atom(), term(), pos_integer(), pos_integer(), atom()) ->
    ok | {error, atom()}.
check_shared_window(Table, Key, WindowMs, MaxEvents, LimitReason) ->
    Now = erlang:system_time(millisecond),
    Bucket = Now div WindowMs,
    BucketKey = {Key, Bucket},
    try ets:update_counter(Table, BucketKey, {2, 1}, {BucketKey, 0}) of
        Count when Count > MaxEvents -> {error, LimitReason};
        _ -> ok
    catch
        error:badarg -> ok
    end.

-spec acquire_connection(term()) -> ok | {error, too_many_connections}.
acquire_connection(PeerIP) when is_binary(PeerIP), PeerIP =/= <<"unknown">> ->
    case rate_limits_disabled() of
        true ->
            ok;
        false ->
            ensure_counter_table(?IP_CONNECTION_TABLE),
            do_acquire_connection(PeerIP)
    end;
acquire_connection(_) ->
    ok.

-spec do_acquire_connection(binary()) -> ok | {error, too_many_connections}.
do_acquire_connection(PeerIP) ->
    try ets:update_counter(?IP_CONNECTION_TABLE, PeerIP, {2, 1}, {PeerIP, 0}) of
        Count when Count > ?MAX_CONNECTIONS_PER_IP ->
            _ = ets:update_counter(?IP_CONNECTION_TABLE, PeerIP, {2, -1, 0, 0}),
            {error, too_many_connections};
        _ ->
            ok
    catch
        error:badarg -> ok
    end.

-spec release_connection(term()) -> ok.
release_connection(PeerIP) when is_binary(PeerIP), PeerIP =/= <<"unknown">> ->
    case ets:whereis(?IP_CONNECTION_TABLE) of
        undefined ->
            ok;
        _ ->
            decrement_connection(PeerIP)
    end;
release_connection(_) ->
    ok.

-spec decrement_connection(binary()) -> ok.
decrement_connection(PeerIP) ->
    try update_connection_count(PeerIP) of
        ok -> ok
    catch
        error:badarg -> ok
    end.

-spec update_connection_count(binary()) -> ok.
update_connection_count(PeerIP) ->
    case ets:update_counter(?IP_CONNECTION_TABLE, PeerIP, {2, -1, 0, 0}) of
        0 ->
            ets:delete(?IP_CONNECTION_TABLE, PeerIP),
            ok;
        _ ->
            ok
    end.

-spec note_disconnect(state()) -> ok.
note_disconnect(State) ->
    release_connection(maps:get(peer_ip, State, undefined)).

-spec ensure_window_table(atom()) -> ok.
ensure_window_table(Table) ->
    ensure_table(Table).

-spec ensure_counter_table(atom()) -> ok.
ensure_counter_table(Table) ->
    ensure_table(Table).

-spec ensure_table(atom()) -> ok.
ensure_table(Table) ->
    case ets:whereis(Table) of
        undefined -> create_table(Table);
        _ -> ok
    end.

-spec create_table(atom()) -> ok.
create_table(Table) ->
    try
        _ = ets:new(Table, [
            named_table,
            public,
            set,
            {write_concurrency, true},
            {read_concurrency, true}
        ]),
        ok
    catch
        error:badarg -> ok
    end.

-spec rate_limits_disabled() -> boolean().
rate_limits_disabled() ->
    case os:getenv("FLUXER_DISABLE_RATE_LIMITS") of
        "1" -> true;
        "true" -> true;
        "TRUE" -> true;
        _ -> false
    end.

-ifdef(TEST).

check_rate_limit_disabled_by_env_test() ->
    Now = erlang:system_time(millisecond),
    State = #{
        rate_limit_state => #{
            events => lists:duplicate(130, Now),
            op_events => #{presence_update => lists:duplicate(10, Now)}
        }
    },
    OldValue = os:getenv("FLUXER_DISABLE_RATE_LIMITS"),
    os:putenv("FLUXER_DISABLE_RATE_LIMITS", "true"),
    try
        CastState = eqwalizer:dynamic_cast(State),
        ?assertEqual({ok, CastState}, check_rate_limit(CastState, presence_update))
    after
        restore_env("FLUXER_DISABLE_RATE_LIMITS", OldValue)
    end.

restore_env(Key, false) ->
    os:unsetenv(Key);
restore_env(Key, Value) ->
    os:putenv(Key, Value).

with_rate_limits_enabled(Fun) ->
    OldValue = os:getenv("FLUXER_DISABLE_RATE_LIMITS"),
    os:unsetenv("FLUXER_DISABLE_RATE_LIMITS"),
    try
        Fun()
    after
        restore_env("FLUXER_DISABLE_RATE_LIMITS", OldValue)
    end.

shared_ip_rate_blocks_over_limit_test() ->
    with_rate_limits_enabled(fun() ->
        IP = <<"198.51.100.10">>,
        reset_shared_ip(IP),
        assert_shared_ip_rate_allows_limit(IP),
        ?assertEqual({error, ip_rate_limited}, check_shared_ip_rate(IP)),
        reset_shared_ip(IP)
    end).

shared_ip_rate_ignores_unknown_ip_test() ->
    with_rate_limits_enabled(fun() ->
        ?assertEqual(ok, check_shared_ip_rate(<<"unknown">>)),
        ?assertEqual(ok, check_shared_ip_rate(undefined))
    end).

connection_cap_blocks_over_limit_test() ->
    with_rate_limits_enabled(fun() ->
        IP = <<"198.51.100.20">>,
        reset_connections(IP),
        assert_connection_cap_allows_limit(IP),
        ?assertEqual({error, too_many_connections}, acquire_connection(IP)),
        ok = release_connection(IP),
        ?assertEqual(ok, acquire_connection(IP)),
        reset_connections(IP)
    end).

assert_shared_ip_rate_allows_limit(IP) ->
    lists:foreach(
        fun(_) -> ?assertEqual(ok, check_shared_ip_rate(IP)) end,
        lists:seq(1, ?SHARED_IP_RATE_MAX_EVENTS)
    ).

assert_connection_cap_allows_limit(IP) ->
    lists:foreach(
        fun(_) -> ?assertEqual(ok, acquire_connection(IP)) end,
        lists:seq(1, ?MAX_CONNECTIONS_PER_IP)
    ).

connection_release_decrements_test() ->
    with_rate_limits_enabled(fun() ->
        IP = <<"198.51.100.30">>,
        reset_connections(IP),
        ok = acquire_connection(IP),
        ok = release_connection(IP),
        ?assertEqual([], ets:lookup(?IP_CONNECTION_TABLE, IP))
    end).

reset_shared_ip(IP) ->
    case ets:whereis(?SHARED_IP_RATE_TABLE) of
        undefined ->
            ok;
        _ ->
            Now = erlang:system_time(millisecond),
            Bucket = Now div ?SHARED_IP_RATE_WINDOW_MS,
            ets:delete(?SHARED_IP_RATE_TABLE, {IP, Bucket}),
            ok
    end.

reset_connections(IP) ->
    case ets:whereis(?IP_CONNECTION_TABLE) of
        undefined ->
            ok;
        _ ->
            ets:delete(?IP_CONNECTION_TABLE, IP),
            ok
    end.

-endif.
